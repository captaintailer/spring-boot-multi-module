// 스프링 관련 플러그인 설정은 루트 프로젝트에서는 적용안함
plugins {
    id 'org.springframework.boot' version '3.2.1' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id "io.freefair.lombok" version "8.4" apply false
}

// 프로젝트 전체에 동일한 라이브러리 버전을 사용할 수 있도록 관리
ext {
    set('springCloudVersion', "2023.0.0")
    set('modelMapperVersion', "3.2.0")
    set('mapStructVersion', "1.5.5.Final")
    set('myBatisSpringBootStarterVersion', "3.0.3")
    set('springdocVersion', "2.3.0")

}

allprojects {
    group = 'dev.tailer.example'
    version = '0.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {

        // MapStruct
        implementation("org.mapstruct:mapstruct:${mapStructVersion}")
        annotationProcessor("org.mapstruct:mapstruct-processor:${mapStructVersion}")
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }
}
